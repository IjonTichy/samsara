actor Ettin2 : Ettin replaces Ettin
{
    +NOTDMATCH
    +NOICEDEATH
    DamageFactor "QuakePistol", 1.5
    DamageFactor "MarathonPistol", 1.5
    DamageFactor "DukePistol", 3
    DamageFactor "Pistol", 3
    DamageFactor "DoomFist", 3
    DamageFactor "ZorchPistol", 3
    DamageFactor "Wraithverge", 1.2
    DamageFactor "Normal", 1.15
    DamageFactor "DragonClaw", 1.45
    DamageFactor "Mace", 1.15
    DamageFactor "Zorch", 1.15
    DamageFactor "PhasingZorch", 1.15
    DamageFactor "BFG", 1.15
    DamageFactor "BFGSpray", 1.15
    DamageFactor "Fire", 1.15
    DamageFactor "Ice", 1.15
    DamageFactor "Disintegrate", 1.15
    DamageFactor "DukeDamage", 1.15
    DamageFactor "DukeExplosive", 1.05
    DamageFactor "DukeIce", 1.2
    DamageFactor "Explosive", 1.15
    DamageFactor "MarathonFusion", 1.15
    DamageFactor "DoomPlasma", 1.15
    DamageFactor "CorvusCrossbow", 1.15
    DamageFactor "CorvusHellstaff", 1.15
    DamageFactor "CorvusHellstaffTomed", 1.15
    DamageFactor "MarathonPlasma", 1.15
    DamageFactor "BJSpear", 1.15
    DamageFactor "WMCBeam", 1.15
    DamageFactor "WMCNade", 1.15
    PainChance "MightyBoot", 255
    BloodType "SamsaraBloodyHellBlood"
    States
    {
      See:
        TNT1 A 0 A_SetTranslucent(1,0)
        ETTN ABCD 5 A_Chase
        Loop

      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        goto "Super::Death"

      XDeath:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        goto "Super::XDeath"

      Death.ZorchPistol:
      Death.Zorch:
      Death.PhasingZorch:
      Death.ZorchKamikaze:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_Stop
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        ETTN H 3 A_SpawnItem("ZorchEffectSmall")
        ETTN H 3 A_SetTranslucent(0.9,0)
        TNT1 A 0 A_PlaySound("PuppyBeat")
        ETTN H 3 A_SetTranslucent(0.8,0)
        TNT1 A 0 A_NoBlocking
        ETTN H 3 A_SetTranslucent(0.7,0)
        ETTN H 3 A_SetTranslucent(0.6,0)
        ETTN H 3 A_SetTranslucent(0.5,0)
        ETTN H 3 A_SetTranslucent(0.4,0)
        ETTN H 3 A_SetTranslucent(0.3,0)
        ETTN H 3 A_SetTranslucent(0.2,0)
        ETTN H 3 A_SetTranslucent(0.1,0)
        TNT1 A 1 A_SetTranslucent(1,0)
        TNT1 A -1 A_QueueCorpse
        stop

      Ice:
      Death.HexenIce:

      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_NoBlocking
        ETTN R 0 A_UnsetSolid
        ETTN R 70 A_GenericFreezeDeath
        ETTN R 0 A_IceGuyDie
        ETTN R 1 A_IceGuyDie
        TNT1 A -1
        stop

      Death.DukeDamage:
      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death

      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death

      Death.DukeExplosive:
        TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
        goto Death.DukeDamage

      DukeGibStart:
        ETTN H 1
        TNT1 A 0 A_UnsetSolid
        TNT1 A 0 A_SpawnDebris("DukeGibs1")
        TNT1 A 0 A_SpawnDebris("DukeGibs2")
        TNT1 A 0 A_SpawnDebris("DukeGibs3")
        TNT1 A 0 A_SpawnDebris("DukeGibs4")
        TNT1 A 0 A_SpawnDebris("DukeGibs5")
        TNT1 A 0 A_SpawnDebris("DukeGibs6")
        TNT1 A 0 A_PlaySound("duke/enemygib")
        TNT1 A 0 A_Jump(96,"XDeath")
        TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
        goto XDeath

      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Ice
        
      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        ETTN H 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        ETTN H 16 A_Pain
        Goto See

      Melee:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        goto "Super::Melee"
    }
}

actor EttinMash2 : Ettin2 replaces EttinMash
{
    +NOBLOOD
    +GHOST
    +NOICEDEATH
    RenderStyle Translucent
    Alpha 0.4
    States
    {
      Death:
      XDeath:
      Ice:
      Death.HexenIce:
      Death.DukeIce:
      Death.DukeExplosive:
      Death.ZorchPistol:
      Death.Zorch:
      Death.PhasingZorch:
      Death.ZorchKamikaze:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        Stop
    }
}