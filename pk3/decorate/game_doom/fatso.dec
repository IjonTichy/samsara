actor Fatso2 : Fatso replaces Fatso
{
    DamageFactor "CorvusCrossbow", 1.1
    DamageFactor "DragonClaw", 1.5
    DamageFactor "MarathonFusion", 2.0
    PainChance "MightyBoot", 255
	BloodType "SamsaraBloodyHellBlood"
    +DONTRIP
    +BOSSDEATH
    +NOTDMATCH
    +NOICEDEATH
    states
    {
      Death.DukeExplosive:
      Death.DukeSpectral:

      Death.DukeDamage:

      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death

      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death

      Death:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        goto "Super::Death"

      Pain.MightyBoot:
        TNT1 A 0 A_ChangeFlag("NOPAIN",1)
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        FATT J 2 A_FaceTarget
        TNT1 A 0 A_Recoil(8)
        FATT J 16 A_Pain
        goto See
		
	  Pain.MarathonStun:
        TNT1 A 0 A_Stop
		TNT1 A 0 A_GiveInventory("NothingSpeed")
        TNT1 A 0 A_PlaySoundEx("marathon/tech50/stun","body",0,0)
        FATT J 5 A_FaceTarget
		TNT1 A 0 A_Gravity
        FATT J 30 A_Pain
        FATT J 70
        Goto See

      Missile:
        TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        goto "Super::Missile"

      Death.ZorchPistol:

      Death.Zorch:
      Death.ZorchKamikaze:
      Death.PhasingZorch:
      Death.ZorchSpectral:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        FATT J 0 A_Stop
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        FATT J 4 A_SpawnItem("ZorchEffectWide")
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        FATT J 4 A_SetTranslucent(0.9,0)
        FATT J 0 A_Pain
        FATT J 4 A_SetTranslucent(0.8,0)
        FATT J 0 A_NoBlocking
        FATT J 4 A_SetTranslucent(0.7,0)
        FATT J 4 A_SetTranslucent(0.6,0)
        FATT J 4 A_SetTranslucent(0.5,0)
        FATT J 4 A_SetTranslucent(0.4,0)
        FATT J 4 A_SetTranslucent(0.3,0)
        FATT J 4 A_SetTranslucent(0.2,0)
        FATT J 4 A_SetTranslucent(0.1,0)
        TNT1 A 1 A_SetTranslucent(1,0)
        TNT1 A -1 A_BossDeath
        stop

      Raise:
        TNT1 A 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
        FATT RQPONMLK 5
        goto See

      ZorchRaise:
        TNT1 A 1 A_UnsetShootable
        TNT1 A 1 A_SpawnItem("SpawnFire2")
        TNT1 A 0 A_PlaySound("archvile/resummon")
        TNT1 A 1 A_TakeInventory("EnemyZorched",1)
        TNT1 A 0 A_SetTranslucent(0.1,0)
        FATT A 3 A_SetTranslucent(0.2,0)
        FATT A 3 A_SetTranslucent(0.3,0)
        FATT A 3 A_SetTranslucent(0.4,0)
        FATT A 3 A_SetTranslucent(0.5,0)
        FATT A 3 A_SetTranslucent(0.6,0)
        FATT A 3 A_SetTranslucent(0.7,0)
        FATT A 3 A_SetTranslucent(0.8,0)
        FATT A 3 A_SetTranslucent(0.9,0)
        FATT A 3 A_SetTranslucent(1,0)
        FATT A 1 A_SetShootable
        Goto See+1

      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)

      Ice:
      Death.HexenIce:

      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
        TNT1 A 0 A_GiveInventory("EnemyZorched",1)
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_SpawnItemEx("FatsoFrozen",0,0,0,0,0,0,0,32)
        //FATT J 0 A_GenericFreezeDeath
        //FATT J 70 A_UnsetSolid
        //FATT J 0 A_IceGuyDie
        //FATT J 1 A_IceGuyDie
        TNT1 A -1 A_BossDeath
        stop
    }
}

actor FatsoFrozen
{
  Health 10
  Radius 48
  Height 64
  Mass 1000
  Monster
  +FLOORCLIP
  +NEVERRESPAWN
  +NOTELEPORT
  +NOTARGET
  States
  {
  Spawn:
    FATT A 1
    FATT J 0 A_GenericFreezeDeath
    FATT J 70 A_UnsetSolid
  Death:
    FATT J 0 A_IceGuyDie
    FATT J 1 A_IceGuyDie
    TNT1 A -1
    stop
}
}

actor FatShot2 : FatShot replaces FatShot
{
    +THRUGHOST
    +FIREDAMAGE
}