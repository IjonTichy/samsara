actor FusionChargeLevel : Inventory { -INVBAR +INVENTORY.UNDROPPABLE Inventory.MaxAmount 35 }
actor FusionChargan : Inventory { -INVBAR +INVENTORY.UNDROPPABLE Inventory.MaxAmount 7 }
actor UsingFusionPistol : Inventory { -INVBAR +INVENTORY.UNDROPPABLE Inventory.MaxAmount 1 }

actor "Fusion Pistol" : Weapon// replaces PlasmaRifle
{
    Weapon.BobRangeX 0
    Weapon.BobStyle Smooth
    Weapon.PreferredSkin "SOFusionSkin"
    Weapon.SelectionOrder 1300
    Weapon.SlotNumber 3
    Inventory.PickupMessage "You picked up a Zeus Class Fusion Pistol!"
    Weapon.AmmoType		"FusionBullet"
    Weapon.AmmoGive		0
    Weapon.AmmoUse		1
    Scale 0.35
    +AMMO_OPTIONAL
    +NOALERT
    +INVENTORY.UNDROPPABLE
    Radius				12
    Height				12
    States
    {
      Spawn:
        FUSP A -1
        STOP

      Ready:
        FUSN A 1 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("FusionBullet",1,"Ready")

      Reload:
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        goto Ready
        FUSN A 0 A_PlaySound("marathon/fusion/reload")
        FUSN A 1 A_Lower
        FUSN A 0 A_Lower
        FUSN A 1 A_Lower
        FUSN A 0 A_Lower
        FUSN A 1 A_Lower
        FUSN A 0 A_Lower
        FUSN A 1 A_Lower
        FUSN A 0 A_Lower
        FUSN A 1 A_Lower
        FUSN A 0 A_Lower
        FUSN A 1 A_Lower
        FUSN A 0 A_Lower
        FUSN A 1 A_Lower
        FUSN A 0 A_Lower
        FUSN A 1 A_Lower
        TNT1 A 25
        FUSN A 1 A_Raise
        FUSN A 0 A_Raise
        FUSN A 1 A_Raise
        FUSN A 0 A_Raise
        FUSN A 1 A_Raise
        FUSN A 0 A_Raise
        FUSN A 1 A_Raise
        FUSN A 0 A_Raise
        FUSN A 1 A_Raise
        FUSN A 0 A_Raise
        FUSN A 1 A_Raise
        FUSN A 0 A_Raise
        FUSN A 1 A_Raise
        //FUSN A 0 A_Raise
        //FUSN A 1 A_Raise
        goto Reload2

      Reload2:
        TNT1 A 0 A_TakeInventory("Cell",1)
        TNT1 A 0 A_GiveInventory("FusionBullet",1)
        TNT1 A 0 A_JumpIfInventory("FusionBullet",0,3)
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        Goto Ready
        TNT1 A 0
        Goto Reload2
        TNT1 A 0
        FUSN A 1
        Goto Ready

      Deselect:
        TNT1 A 0 A_Lower
        FUSN A 1 A_Lower
        LOOP

      Select:
        TNT1 A 0 A_TakeInventory("UsingSMG",1)
        TNT1 A 0 A_TakeInventory("UsingPistols",1)
        TNT1 A 0 A_TakeInventory("UsingFists",1)
        TNT1 A 0 A_TakeInventory("UsingShotguns",1)
        //TNT1 A 0 A_TakeInventory("UsingFusionPistol",1)
        TNT1 A 0 A_TakeInventory("UsingAssaultRifle",1)
        TNT1 A 0 A_TakeInventory("UsingSpanker",1)
        TNT1 A 0 A_TakeInventory("UsingToasty",1)
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon",1)
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon2",1)
        TNT1 A 0 A_TakeInventory("UsingWMC",1)
        TNT1 A 0 A_TakeInventory("UsingDualPistols",1)
        TNT1 A 0 A_TakeInventory("UsingDualShotguns",1)
        
        TNT1 A 0 A_GiveInventory("UsingFusionPistol",1)
        TNT1 A 0 A_Raise
        FUSN A 1 A_Raise
        LOOP

      Fire:
        TNT1 A 0 A_JumpIf(waterlevel == 3, "WaterFire1")
        FUSN A 1
        TNT1 A 0 A_JumpIfNoAmmo("Reload")
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"CoopFire")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        TNT1 A 0 A_AlertMonsters//,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("FusionShot",random(-1,1),1,0,6,0,random(-1,1))
        TNT1 A 0 A_PlaySound("marathon/fusion/fire")
        FUSN A 2 A_Light2
        FUSN B 2 Bright A_Light1 
        FUSN C 2 A_Light0
        FUSN D 3
        TNT1 A 0 A_Refire
        Goto Ready

      CoopFire:
        TNT1 A 0 A_AlertMonsters//,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("FusionShotCoop",random(-1,1),1,0,6,0,random(-1,1))
        TNT1 A 0 A_PlaySound("marathon/fusion/fire")
        FUSN A 2 A_Light2
        FUSN B 2 Bright A_Light1 
        FUSN C 2 A_Light0
        FUSN D 3
        TNT1 A 0 A_Refire
        Goto Ready

      AltFire:
        TNT1 A 0 A_JumpIfInventory("FusionBullet",1,1)
        goto Ready
        FUSN A 1 A_PlaySoundEx("marathon/fusion/charge","soundslot7",0,0)
        TNT1 A 0 A_GiveInventory("FusionChargan",3)

      AltHold:
        TNT1 A 0 A_JumpIfInventory("FusionChargan",7,"Chargan")
        FUSN A 5
        TNT1 A 0 A_GiveInventory("FusionChargan",1)
        TNT1 A 0 A_Refire
        TNT1 A 0 A_JumpIfInventory("FusionChargeLevel",1,"AltChargeFire")
        TNT1 A 0 A_TakeInventory("FusionChargan",7)
        goto Ready

      AltChargeFire:
        TNT1 A 0 A_JumpIf(waterlevel == 3, "WaterFire2")
        FUSN A 1
        TNT1 A 0 A_JumpIfInventory("SPModeOn",1,"AltCoopFire")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"AltCoopFire")
        TNT1 A 0 A_AlertMonsters//,0,0,0,0)
        TNT1 A 0 A_TakeInventory("FusionBullet",4)
        TNT1 A 0 A_TakeInventory("FusionChargan",7)
        TNT1 A 0 A_TakeInventory("FusionChargeLevel",35)
        TNT1 A 0 A_FireCustomMissile("FusionSuper",random(-1,1),0,0,6,0,random(-1,1))
        TNT1 A 0 A_PlayWeaponSound("marathon/fusion/super")
        FUSN A 4 A_Light2
        FUSN B 4 Bright A_Light1
        FUSN C 4 A_Light0
        FUSN D 4
        Goto Ready

      AltCoopFire:
        TNT1 A 0 A_AlertMonsters//,0,0,0,0)
        TNT1 A 0 A_TakeInventory("FusionBullet",4)
        TNT1 A 0 A_TakeInventory("FusionChargan",7)
        TNT1 A 0 A_TakeInventory("FusionChargeLevel",35)
        TNT1 A 0 A_FireCustomMissile("FusionSuperCoop",random(-1,1),0,0,6,0,random(-1,1))
        TNT1 A 0 A_PlayWeaponSound("marathon/fusion/super")
        FUSN A 4 A_Light2
        FUSN B 4 Bright A_Light1
        FUSN C 4 A_Light0
        FUSN D 4
        Goto Ready

      Chargan:
        TNT1 A 0 A_JumpIfInventory("FusionChargeLevel",34,"WhoopsieDaisy")
        TNT1 A 0 A_PlayWeaponSound("marathon/fusion/beep")
        FUSN E 2 A_TakeInventory("FusionChargan",7)
        TNT1 A 0 A_GiveInventory("FusionChargeLevel",1)
        goto AltHold

      WhoopsieDaisy:
        TNT1 A 0 A_TakeInventory("FusionChargan",7)
        TNT1 A 0 A_TakeInventory("FusionChargeLevel",35)
        TNT1 A 0 A_TakeInventory("FusionBullet",20)
        TNT1 A 0 A_SetBlend("Blue",0.8,35)
        TNT1 A 0 A_PlayWeaponSound("hunter/explode")
        TNT1 A 0 DamageThing(999)//A_Explode(500,256) - Turns out people can grief and kill each other.
        Goto Ready

      WaterFire1:
        FUSN A 1
        TNT1 A 0 A_JumpIfNoAmmo("Ready")
        TNT1 A 0 A_AlertMonsters//,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("FusionShotWater",random(-1,1),1,0,6,0,random(-1,1))
        TNT1 A 0 A_PlaySound("marathon/fusion/fire")
        FUSN A 2 A_Light2
        FUSN B 2 Bright A_Light1 
        FUSN C 2 A_Light0
        FUSN D 3
        TNT1 A 0 A_Refire
        goto Ready

      WaterFire2:
        FUSN A 1
        TNT1 A 0 A_AlertMonsters//,0,0,0,0)
        TNT1 A 0 A_TakeInventory("FusionBullet",4)
        TNT1 A 0 A_TakeInventory("FusionChargan",7)
        TNT1 A 0 A_TakeInventory("FusionChargeLevel",35)
        TNT1 A 0 A_FireCustomMissile("FusionShotWater2",random(-1,1),0,0,6,0,random(-1,1))
        TNT1 A 0 A_PlayWeaponSound("marathon/fusion/super")
        FUSN A 2 A_Light2
        FUSN B 2 Bright A_Light1 
        FUSN C 2 A_Light0
        FUSN D 3
        TNT1 A 0 A_Refire
        goto Ready
    }
}

actor FusionBullet : Ammo
{
    +IGNORESKILL
    Inventory.MaxAmount 20
}

actor FusionShot
{
    Decal DoomImpScorch
    Radius 6
    Height 8
    Speed 25
    Damage (30+random(0,10))
    DamageType "MarathonFusion"
    Scale 0.35
    DeathSound "marathon/fusion/hit"
    +FOILINVUL
    +PIERCEARMOR
    Obituary "%k \cnfused\c- %o \cnwith the dirt.\c-"
    Projectile
    States
    {
      Spawn:
        FSHT ABC 2 Bright
        LOOP

      Death:
        FHIT ABCD 2 Bright
        STOP
    }
}

actor FusionShotCoop : FusionShot
{
    Speed 20
    +THRUSPECIES
    Species "Player"
}

actor FusionShotWater
{
    Decal DoomImpScorch
    Radius 6
    Height 8
    Speed 20
    Damage 0//(30+random(0,10))
    DamageType "MarathonFusion"
    Scale 0.35
    DeathSound "marathon/fusion/hit"
    +FOILINVUL
    Obituary "%k \cnfused\c- %o \cnwith the dirt.\c-"
    Projectile
    States
    {
      Spawn:
        FSHT A 2 Bright

      Death:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_Explode(random(30,40),128)
        FHIT ABCD 2 Bright
        STOP
    }
}

actor FusionSuper
{
    Decal Scorch
    Radius 8
    Height 8
    Speed 35
    Damage (80+random(0,20))
    DamageType "MarathonFusion"
    Scale 0.75
    DeathSound "marathon/fusion/hit"
    +FOILINVUL
    +PIERCEARMOR
    Obituary "%k \cnfused\c- %o \cnwith the dirt.\c-"
    Projectile
    States
    {
      Spawn:
        CHGB ABC 3 Bright A_SpawnItem("FusionTrail")
        LOOP

      Death:
        FHIT ABCD 2 Bright
        STOP
    }
}

actor FusionSuperCoop : FusionSuper
{
    Speed 30
    +THRUSPECIES
    Species "Player"
}

actor FusionTrail
{
    +RANDOMIZE
    +NOCLIP
    +NOGRAVITY
    Speed 0
    Scale 0.75
    +CLIENTSIDEONLY
    states
    {
      Spawn:
        TNT1 A 2
        FSCT ABC 2 BRIGHT
        stop
    }
}

actor FusionShotWater2
{
    Decal Scorch
    Radius 6
    Height 8
    Speed 30
    Damage 0//(80+random(0,20))
    DamageType "MarathonFusion"
    Scale 0.75
    DeathSound "marathon/fusion/hit"
    +FOILINVUL
    Obituary "%k \cnfused\c- %o \cnwith the dirt.\c-"
    Projectile
    States
    {
      Spawn:
        CHGB A 2 Bright

      Death:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_Explode(random(80,100),128)
        FHIT ABCD 4 Bright
        STOP
    }
}

